cmake_minimum_required(VERSION 3.20.0)
project(amanomiko_patch VERSION 0.1.1)

set(CMAKE_CXX_STANDARD 20)
set(SOURCE_DIR "${CMAKE_CURRENT_LIST_DIR}/src")

if(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
	
	set(IS_CLANG_COMPILER 1)
	add_compile_options(
		"-Wno-braced-scalar-init"
        "-fasm-blocks"
		"-m32"
	)
	add_link_options("-Wl")

elseif(CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
	
	set(IS_MSVC_COMPILER 1)
	set(CMAKE_CXX_FLAGS_RELEASE "/O1 /Ob2 /DNDEBUG")
    set(DEFAULT_LINK_FLAGS "/DYNAMICBASE:NO")

	add_compile_options(
        "/source-charset:utf-8"
        "$<$<CONFIG:Release>:/GF>"
        "$<$<CONFIG:Release>:/Oy>"
        "$<$<CONFIG:Release>:/GL>"
        "$<$<CONFIG:Release>:/GS->"
    )

    if(${CMAKE_BUILD_TYPE} STREQUAL "Release")
		set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded")
		set(YY_THUNKS_ENTRY "/ENTRY:DllMainCRTStartupForYY_Thunks")
	elseif(${CMAKE_BUILD_TYPE} STREQUAL "Debug")
		set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreadedDebug")
	endif()
	
	# 安装 VC-LTL5（可选）
	# https://github.com/Chuyu-Team/VC-LTL5/releases/latest
	include("${CMAKE_CURRENT_LIST_DIR}/msvc/VC-LTL helper for cmake.cmake")

	set(YY_THUNKS_Win2K_OBJ "${CMAKE_CURRENT_LIST_DIR}/msvc/YY_Thunks_for_Win2K.obj.bin")
	set(CMAKE_CXX_STANDARD_LIBRARIES "\"${YY_THUNKS_Win2K_OBJ}\" ${CMAKE_CXX_STANDARD_LIBRARIES}")
    set(CMAKE_C_STANDARD_LIBRARIES   "\"${YY_THUNKS_Win2K_OBJ}\" ${CMAKE_C_STANDARD_LIBRARIES}")
    add_link_options("-SUBSYSTEM:CONSOLE,5.01")

endif()

add_subdirectory("${SOURCE_DIR}/fontmanager")

add_library(${PROJECT_NAME} SHARED
	"${SOURCE_DIR}/amanomiko_patch.c"
	"${SOURCE_DIR}/amanomiko_patch.cpp"
)
target_link_libraries(${PROJECT_NAME}
	FontManager
)

if(${IS_MSVC_COMPILER})
	set_target_properties(${PROJECT_NAME} PROPERTIES 
		LINK_FLAGS "${YY_THUNKS_ENTRY} ${DEFAULT_LINK_FLAGS}"
	)
endif()

set(DVFS_INCLUDE "${SOURCE_DIR}/src/compat/dvfs")
file(GLOB DVFS_HEADERS "${DVFS_INCLUDE}/*.h")
if(DVFS_HEADERS)
    target_compile_definitions(${PROJECT_NAME} PRIVATE "USE_DVFS")
else()
	set(DVFS_INCLUDE "")
endif()

target_include_directories(${PROJECT_NAME} 
	PUBLIC "${DVFS_INCLUDE}"
	"${SOURCE_DIR}/compat"
)

string(TIMESTAMP BUILD_DATE "%Y-%m-%d")
target_compile_definitions(${PROJECT_NAME} PRIVATE 
	"PROJECT_NAME=\"${PROJECT_NAME}\""
	"PROJECT_VERSION=\"${PROJECT_VERSION}\""
	"PROJECT_BUILD_TIME=\"${BUILD_DATE}\""
	"_CRT_SECURE_NO_WARNINGS"
	"USE_COMPAT"
)

include(libfontmanager.cmake)